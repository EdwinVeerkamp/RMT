apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'idea'

repositories {
    mavenCentral()
    mavenRepo name: "Spring Maven MILESTONE Repository", urls: ["http://maven.springframework.org/milestone"]
}

configurations {
    provided
}

// version properties
version_spring = "3.0.6.RELEASE"
version_wicket = "1.5.1"
version_hibernate = "3.6.7.Final"
version_scala = "2.9.1"


dependencies {
    compile(
            'org.springframework:spring-core:' + version_spring,
            'org.springframework:spring-context:' + version_spring,
            'org.springframework.security:spring-security-web:' + version_spring,
            'org.apache.wicket:wicket-core:' + version_wicket,
            'org.apache.wicket:wicket-extensions:' + version_wicket,
            'org.apache.wicket:wicket-spring:' + version_wicket,
            'org.apache.wicket:wicket-datetime:' + version_wicket,
            'org.apache.wicket:wicket-devutils:' + version_wicket,
            'org.hibernate:hibernate-validator:4.2.0.Final',
            'org.hibernate:hibernate-entitymanager:' + version_hibernate,
            'org.springframework.data:spring-data-jpa:1.0.1.RELEASE',
            'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final',
            'org.hibernate:hibernate-core:' + version_hibernate,
            'commons-beanutils:commons-beanutils:1.8.3',
            'commons-collections:commons-collections:3.2.1',
            'org.apache.commons:commons-lang3:3.0.1',
            'ch.qos.logback:logback-classic:0.9.30',
            'org.apache.httpcomponents:httpclient:4.1.2',
            'com.google.code.gson:gson:1.7.2',
            // TODO - jar needed for idea to use apt during compilation. possible to move to other configuration?
            'org.hibernate:hibernate-jpamodelgen:1.1.1.Final' ,
            'joda-time:joda-time:2.0',
            'joda-time:joda-time-hibernate:1.3',
            'org.joda:joda-convert:1.1',
            'com.thoughtworks.xstream:xstream:1.4.1'
    )
    runtime(
            'org.slf4j:jcl-over-slf4j:1.6.2',
            'commons-dbcp:commons-dbcp:1.4',
            'org.hibernate:hibernate-core:' + version_hibernate,
            'mysql:mysql-connector-java:5.1.16',
            'org.springframework.security:spring-security-web:' + version_spring,
            'org.springframework.security:spring-security-config:' + version_spring
    )
    provided(

    )
    testCompile(
            'org.springframework:spring-test:' + version_spring,
            'com.h2database:h2:1.3.160',
            'org.dbunit:dbunit:2.4.8',
            'org.scala-lang:scala-library:' + version_scala,
            'org.testng:testng:6.1.1', // current rel. 6.2.1 has dependency to com.google.inject
            'org.scalatest:scalatest:1.4.RC2',
            'org.mockito:mockito-core:1.9.0-rc1',
            'javax.servlet:servlet-api:2.5'
    )
    scalaTools(
            'org.scala-lang:scala-compiler:' + version_scala,
            'org.scala-lang:scala-library:' + version_scala
    )

}

// wicket app: need to copy .html files from java-src to output
sourceSets.main.resources.srcDirs('/src/main/java')
sourceSets.test.resources.srcDirs('/src/test/java')
sourceSets.test.resources.srcDirs('/src/test/scala')

// add directory with db-unit files to test-classpath
sourceSets.test.resources.srcDirs('/src/test/database')
/**
 * It's currently not possible to capture the console output of the tests.
 * Until it is fixed (http://issues.gradle.org/browse/GRADLE-1009) use the
 * log-file.
 */
test {
    useTestNG()
}

/**
 * Use this task to update idea project file with dependencies.
 * It will remove existing dependencies before updating them.
 */
ideaModule {
    downloadJavadoc = true
    beforeConfigured { module ->
        module.dependencies.clear()
    }
}

// Find any 3rd party libraries which have released new versions
// to the central Maven repo since we last upgraded.
task checkLibVersions << {
    def checked = [:]
    allprojects {
        configurations.each { configuration ->
            configuration.allDependencies.each { dependency ->
                def version = dependency.version
                if (!version.contains('SNAPSHOT') && !checked[dependency]) {
                    def group = dependency.group
                    def path = group.replace('.', '/')
                    def name = dependency.name
                    def url = "http://repo1.maven.org/maven2/$path/$name/maven-metadata.xml"
                    try {
                        def metadata = new XmlSlurper().parseText(url.toURL().text)
                        def versions = metadata.versioning.versions.version.collect { it.text() }
                        versions.removeAll { it.toLowerCase().contains('alpha') }
                        versions.removeAll { it.toLowerCase().contains('beta') }
                        versions.removeAll { it.toLowerCase().contains('rc') }
                        def newest = versions.max()
                        if (version != newest) {
                            println "$group:$name $version -> $newest"
                        }
                    } catch (FileNotFoundException e) {
                        logger.debug "Unable to download $url: $e.message"
                    } catch (org.xml.sax.SAXParseException e) {
                        logger.debug "Unable to parse $url: $e.message"
                    }
                    checked[dependency] = true
                }
            }
        }
    }
}